import io.gitlab.arturbosch.detekt.Detekt

plugins {
    id 'com.android.application' version '7.3.0-beta05' apply false
    id 'com.android.library' version '7.3.0-beta05' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.0' apply false
    id "io.gitlab.arturbosch.detekt" version '1.20.0'
}

def projectSource = file(projectDir)
def kotlinFiles = "**/*.kt"
def resourceFiles = "**/resources/**"
def buildFiles = "**/build/**"

tasks.register("detektAll", Detekt) {

    description = "Custom DETEKT build for all modules"
    parallel = true
    ignoreFailures = true
    autoCorrect = false
    buildUponDefaultConfig = true
    setSource(projectSource)
    include(kotlinFiles)
    exclude(resourceFiles, buildFiles)
    it.classpath.setFrom(project.configurations.getByName("detekt"))

    reports {
        html.enabled = true
        xml.enabled = true
        txt.enabled = false
    }
}

tasks.register("detektGenerateBaseline", io.gitlab.arturbosch.detekt.DetektCreateBaselineTask) {
    description = "Custom DETEKT build to build baseline for all modules"
    parallel = true
    ignoreFailures = false
    buildUponDefaultConfig = true
    setSource(projectSource)
    include(kotlinFiles)
    exclude(resourceFiles, buildFiles)
    it.classpath.setFrom(project.configurations.getByName("detekt"))
}